 uri "http://sadl.org/ML.sadl" alias ml.
 
 import "http://sadl.org/data.sadl".
 import "http://sadl.org/models.sadl".
 import "http://sadl.org/ScientificConcepts3.sadl".
 
 External unitResolver(string u, ...) returns string: "http://sadl.org/unitSelector". 
 External derivative(ScientificConcept numerator, ScientificConcept denominator, int n) returns decimal, string: 
 	"http://sadl.org/derivative".

 ^Equation is a class.	// this is defined in sadl base model and should be available.
 
 MLModel is a type of ^Model,
 	described by concept with values of type ScientificConcept,
 	described by representation with a single value of type ^Equation,
 	described by trainingData with values of type TabularDataSet.
 	
 KChainModel is a type of ^Model.
 {PhysicsModel, MLModel} are types of KChainModel.
 // Composition of models
 // Variations of a model NSL, NSL-constant-mass
  	
 	
 MFVData1 is a TabularDataSet,
 	has column (a Column ml:Time with name "timestamp", with ^type "xsd:dateTime"),
 	has column (a Column ml:Force with name "Force", with ^type Force),
 	has column (a Column ml:Mass with name "Mass", with ^type Mass),
 	has column (a Column ml:Velocity with name "Velocity", with ^type Velocity),
 	has location "http://nsk1websadl01.crd.ge.com/aske/data/mfvdata1.csv".
 	
// This is created as an exploratory concept 	
 MassForceVelocityModel is an MLModel,
 	has concept Force,
 	has concept Mass,
 	has concept Velocity.
 	
// This query follows (	
 Ask: select ds where ds is a TabularDataSet and ds has column c1 and c1 has ^type Force and
 	ds has column c2 and c2 has ^type Mass and ds has column c3 and c3 has ^type Velocity.	
 
 Ask: select distinct ds where ds is a TabularDataSet and 
 	ds has column c1 and c1 has ^type (concept of MassForceVelocityModel) and
 	ds has column c2 and c2 has ^type (concept of MassForceVelocityModel) and
 	ds has column c3 and c3 has ^type (concept of MassForceVelocityModel).	
 	
// these are added to model 	
 Equation newtons2ndLaw 
 	(note "Force is equal to the derivative of momentum with respect to time.") 
 	(Mass m, Velocity v) returns Force: 
 	a Force f with ^value fv, with unit fu
 	
 	return f 
 	
 	where mv is a Momentum with ^value (^value of m * ^value of v), with unit unitResolver("*", unit of m, unit of v) and
 		[fv,fu] = derivative(mv, Time, 1).
 
 MassForceVelocityModel has representation newtons2ndLaw,
 	 	has trainingData MFVData1. 	
  	
 