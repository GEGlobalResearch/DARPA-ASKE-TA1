/*
SADL Extension for DARPA ASKE dialog - © 2014-2018 General Electric Company – All Rights Reserved

DARPA Wording for project

 */
grammar com.ge.research.sadl.darpa.aske.Dialog with com.ge.research.sadl.SADL

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.ge.com/research/sadl/SADL" as sadl

generate dialog "http://www.ge.com/research/sadl/darpa/aske/Dialog"

DialogModel returns sadl::SadlModel :
    super::SadlModel
;

@Override
SadlModelElement returns sadl::SadlModelElement :
	'CM:' (SadlStatement|EquationStatement|ExternalEquationStatement) EOS |
	'CM:' StringResponse EOS |
	ModifiedAskStatement EOS	|
	WhatStatement EOS			|
	HowManyValuesStatement EOS
;

StringResponse:
	str=STRING
;

ModifiedAskStatement returns sadl::ExpressionScope:
	{ModifiedAskStatement} ('Ask' | 'ask') expr=(ConstructExpression | AskExpression | Expression)
;

WhatStatement:
	{WhatStatement} ('What'|'what') stmt=(WhatIsStatement | WhatValuesStatement)
;
WhatIsStatement:
	{WhatIsStatement} 'is' article=AnArticle?  target=SadlResource
;

WhatValuesStatement:
	{WhatValuesStatement} ('value'|'values') typ=('can'|'must') prop=SadlResource 'of' article=AnArticle? cls=SadlPrimaryTypeReference 'have'
;

HowManyValuesStatement:
	{HowManyValuesStatement} ('How'|'how') 'many' 'values' 'of' prop=SadlResource ('of' 'type' typ=SadlPrimaryTypeReference)? 'can' article=AnArticle? cls=SadlPrimaryTypeReference 'have'
;

@Override
EOS:
	super |
	'?'
;
