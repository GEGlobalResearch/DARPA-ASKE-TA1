/********************************************************************** 
 * Note: This license has also been called the "New BSD License" or 
 * "Modified BSD License". See also the 2-clause BSD License.
 *
 * Copyright ï¿½ 2018-2019 - General Electric Company, All Rights Reserved
 * 
 * Projects: ANSWER and KApEESH, developed with the support of the Defense 
 * Advanced Research Projects Agency (DARPA) under Agreement  No.  
 * HR00111990006 and Agreement No. HR00111990007, respectively. 
 *
 * Redistribution and use in source and binary forms, with or without 
 * modification, are permitted provided that the following conditions are met:
 * 1. Redistributions of source code must retain the above copyright notice, 
 *    this list of conditions and the following disclaimer.
 *
 * 2. Redistributions in binary form must reproduce the above copyright notice, 
 *    this list of conditions and the following disclaimer in the documentation 
 *    and/or other materials provided with the distribution.
 *
 * 3. Neither the name of the copyright holder nor the names of its 
 *    contributors may be used to endorse or promote products derived 
 *    from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" 
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE 
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE 
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE 
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR 
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF 
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS 
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) 
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF 
 * THE POSSIBILITY OF SUCH DAMAGE.
 *
 ***********************************************************************/
/*
 * SADL Extension for DARPA ASKE dialog
 */
grammar com.ge.research.sadl.darpa.aske.Dialog with com.ge.research.sadl.SADL

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.ge.com/research/sadl/SADL" as sadl

generate dialog "http://www.ge.com/research/sadl/darpa/aske/Dialog"

DialogModel returns sadl::SadlModel :
    super::SadlModel
;

@Override
SadlModelElement returns sadl::SadlModelElement :
	super::SadlStatement EOS			|
	super::ExternalEquationStatement EOS	|
	super::EquationStatement EOS	|
	AnswerCMStatement EOS 		|
	ModifiedAskStatement EOS	|
	WhatStatement EOS			|
	HowManyValuesStatement EOS	|
	SaveStatement EOS			|
	EvalStatement EOS			|
	YesNoAnswerStatement EOS	|
	MyNameIsStatement EOS		|
	TargetModelName EOS			|
	ExtractStatement EOS		|
	CompareStatement EOS 	 	|
	SuitabilityStatement EOS	
;

CompareStatement:
	('Compare'|'compare') toCompare=Expression compareConnective=('when' | 'for') whenExpr=Expression
;

SuitabilityStatement:
	(('Which'|'which') what=Expression 'is' 'suitable' suitableConnective=('when'|'for') suitableExpression=Expression) |
	(('Is'|'is') what=Expression 'suitable' suitableConnective=('when'|'for') suitableExpression=Expression)
;

ExtractStatement:
	('Extract'|'extract') 'from' sourceURIs+=STRING (',' sourceURIs+=STRING)*
;

EvalStatement returns SadlEquationInvocation:
	{SadlEquationInvocation} ('Evaluate'|'evaluate') name=SadlResource '(' (parameter+=Expression ('{'units+=UNIT'}')? (',' parameter+=Expression ('{'units+=UNIT'}')?)* )?')'
;

TargetModelName:
	'target' 'model' targetResource=[sadl::SadlModel|STRING] ('alias' alias=ID)?
;

MyNameIsStatement:
	('My'|'my') 'name' 'is' answer=ID
;

YesNoAnswerStatement:
	{YesNoAnswerStatement} answer = ('Y'|'y'|'Yes'|'yes'|'N'|'n'|'No'|'no')
;

SaveStatement:
	{SaveStatement} ('Save' | 'save') (target=SadlResource | all='all') ('to' saveTarget=ID)?
;

AnswerCMStatement:
	'CM:' (sstmt = ((SadlStatement|ModifiedAskStatement|WhatStatement|YesNoAnswerStatement|ExternalEquationStatement|EquationStatement)) | str = STRING annotations+=SadlAnnotation*)
;

ModifiedAskStatement returns sadl::ExpressionScope:
	{ModifiedAskStatement} start = ('Ask' | 'ask' | 'Find' | 'find' |'Graph' | 'graph') 
	(
		expr=(ConstructExpression | AskExpression | Expression) (':' '[' parameterizedValues = ValueRow ']')?
	)
;

WhatStatement:
	{WhatStatement} ('What'|'what') stmt=(WhatIsStatement | WhatValuesStatement)
;
WhatIsStatement:
	{WhatIsStatement} 'is' (('your' 'name') | target=Expression WhenPart?)
;

fragment WhenPart returns WithWhenPart:
	'when' when=Expression
;

WhatValuesStatement:
	{WhatValuesStatement} (typof='type' 'of')? ('value'|'values') typ=('can'|'must') prop=SadlResource 'of' article=AnArticle? cls=SadlPrimaryTypeReference 'have'
;

HowManyValuesStatement:
	{HowManyValuesStatement} ('How'|'how') 'many' 'values' 'of' prop=SadlResource ('of' 'type' typ=SadlPrimaryTypeReference)? 'can' article=AnArticle? cls=SadlPrimaryTypeReference 'have'
;

@Override
EOS:
	super |
	'?'
;
