/*
SADL Extension for DARPA ASKE dialog - © 2018-2019 General Electric Company – All Rights Reserved

DARPA Wording for project

 */
grammar com.ge.research.sadl.darpa.aske.Dialog with com.ge.research.sadl.SADL

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.ge.com/research/sadl/SADL" as sadl

generate dialog "http://www.ge.com/research/sadl/darpa/aske/Dialog"

DialogModel returns sadl::SadlModel :
    super::SadlModel
;

@Override
SadlModelElement returns sadl::SadlModelElement :
	SadlStatement EOS 			|
	ResponseStatement EOS 		|
	ModifiedAskStatement EOS	|
	WhatStatement EOS			|
	HowManyValuesStatement EOS
;

ResponseStatement:
	'CM:' (sstmt = ((SadlStatement|EquationStatement|ExternalEquationStatement)) | str = STRING)
;

ModifiedAskStatement returns sadl::ExpressionScope:
	{ModifiedAskStatement} start = ('Ask' | 'ask' | 'Graph' | 'graph') 
	(
//		(name=SadlResource (annotations+=NamedStructureAnnotation)* )? 
		expr=(ConstructExpression | AskExpression | Expression) // | name=SadlResource
	)
;

WhatStatement:
	{WhatStatement} ('What'|'what') stmt=(WhatIsStatement | WhatValuesStatement)
;
WhatIsStatement:
	{WhatIsStatement} 'is' target=Expression WhenPart?
;

fragment WhenPart returns WithWhenPart:
	'when' when=Expression
;

WhatValuesStatement:
	{WhatValuesStatement} (typof='type' 'of')? ('value'|'values') typ=('can'|'must') prop=SadlResource 'of' article=AnArticle? cls=SadlPrimaryTypeReference 'have'
;

HowManyValuesStatement:
	{HowManyValuesStatement} ('How'|'how') 'many' 'values' 'of' prop=SadlResource ('of' 'type' typ=SadlPrimaryTypeReference)? 'can' article=AnArticle? cls=SadlPrimaryTypeReference 'have'
;

@Override
EOS:
	super |
	'?'
;
